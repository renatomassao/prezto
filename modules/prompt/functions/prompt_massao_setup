#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

function prompt_massao_precmd {
  time_now=`date +%H:%M:%S`
  prompt_massao_pwd
  git-info
}

function prompt_massao_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_massao_pwd="$MATCH"
    unset MATCH
  else
    _prompt_massao_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_massao_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_massao_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%F{2}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{2}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{1}✗%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{2}(%f$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s%F{2})%f' \
    'rprompt' '%C%D'

  # Define prompts.
PROMPT='%F{6}%n%f%B%F{1}@%f%b%F{3}${PWD/#$HOME/~}%f${(e)git_info[prompt]}
%F{6}[$time_now]%f %F{4}❄ %f'
# PROMPT='%F{3}${_prompt_massao_pwd}%f${(e)git_info[prompt]} %F{4}❄ %f'
}

prompt_massao_setup "$@"